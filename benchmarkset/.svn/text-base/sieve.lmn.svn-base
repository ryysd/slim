/* 
 *  sieve of eratosthenes
 *
 *  @author  Masato Gocho
 *  @date    2011.02.18
 *
 *  Property0: Respons
 *  Property1: Respons under the Strong Fairness
 */

primary(cur(3), max(10), L).

sv0 @@
  primary(cur(C), max(M), L), 
  proc{recv=[E |F], -L, $p[F |*A], @p} :- unary(E), C2=C+1, C<M |
    primary(cur(C2), max(M), L), 
    proc{recv=[C, E |F], -L, $p[F |*A], @p}.

proc{
  -L, sieve(2), recv=[r_nil], send=[s_nil].

  sv1 @@
    F=[Y, r_nil |[]], sieve(S) :- Y > 0, (Y mod S) =:= 0 |
      F=[r_nil |[]], sieve(S).
  sv2 @@
    F=[Y, r_nil |[]], sieve(S), G=[s_nil |[]] :- Y > 0, (Y mod S) =\= 0 |
      F=[r_nil |[]], sieve(S), G=[Y, s_nil |[]].
}.


gen @@ 
  proc{@p, send=[X |F], $p[F], -P}           :- int(X) |
    proc{@p, send=[X |F], $p[F], -P, +L},
    proc{@p, send=[s_nil], recv=[r_nil], -L, sieve(X)}.
snd @@ 
  proc{@p, send=[X |F], $p[F], -P, +L},  
  proc{@q, recv=[Y |E], $q[E |*A], -L}       :- int(X), unary(Y) |
    proc{@p, send=F, $p[F], -P, +L}, 
    proc{@q, recv=[X, Y |E], $q[E |*A], -L}. 